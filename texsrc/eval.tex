\chapter{Evaluation}

\hrulefill

\textbf{IMPORTANT NOTE:} Some of the discussions below refer to an older draft of the
RISC-V Bitmanip extension and are now out-of-date.

\hrulefill

This chapter contains a collection of short code snippets and algorithms using
the Bitmanip extension for evaluation purposes. For the sake of simplicity we
assume RV32 for most examples in this chapter.

Most assembler routines in this chapter are written as if they were ABI functions,
i.e. arguments are passed in a0, a1,~\dots~and results are returned in a0. Registers
a0, a1,~\dots~are also used for spilling Registers t0, t1,~\dots~are used for
pre-computed masks to be used with {\tt bext}/{\tt bdep}.

Some of the assembler routines below can not or should not overwrite their
first argument. In those cases the arguments are passed in a1, a2,~\dots~and
results are returned in a0.

The main motivation behind this chapter is to show that all the common bit
manipulation tasks can be performed in few instructions using Bitmanip. (In
many cases RV32I/RV64I instructions are already sufficient.) In most cases the
sequences are short enough to allow large cores to macro-op-fuse them into a
single instruction. For this reason we also focus on code snippets that do
not spill any registers, as this further simplifies macro-op fusion.

There likely will be a separate RISC-V standard for recommended sequences for
macro-op fusion. The macros listed here are merely for demonstrating that
suitable sequences exist. We do not advocate for any of those sequences to
become ``standard sequences'' for macro-op fusion.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Bitfield extract}

Extracting a bit field of length {\tt len} at position {\tt pos} can be done using
two shift operations (or equivalently using the {\tt bfext} pseudo instruction, see
Section~\ref{bfext}).

\begin{verbatim}
  slli a0, a0, (XLEN-len-pos)
  srli a0, a0, (XLEN-len)
\end{verbatim}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{MIX/MUX pattern}
\label{mixmux}

A MIX pattern selects bits from {\tt a0} and {\tt a1} based on the bits in
the control word {\tt a2}.

\begin{verbatim}
  and a0, a0, a2
  andn a1, a1, a2
  or a0, a0, a1
\end{verbatim}

A MUX operation selects word {\tt a0} or {\tt a1} based on if the control
word {\tt a2} is zero or nonzero, without branching.

\begin{verbatim}
  snez a2, a2
  neg a2, a2
  and a0, a0, a2
  andn a1, a1, a2
\end{verbatim}

Or when {\tt a2} is already either 0 or 1:

\begin{verbatim}
  neg a2, a2
  and a0, a2
  andn a1, a1, a2
  or a0, a0, a1
\end{verbatim}

Alternatively, a core might fuse a conditional branch that just skips one
instruction with that instruction to form a fused conditional macro-op.

A core with support for ternary instructions provides dedicated instructions
for those operations: {\tt cmix} and {\tt cmov}. All examples below use those
dedicated instructions. For cores without support for ternary operations, those
instances of {\tt cmix} and {\tt cmov} must be replaced by the above code
patterns.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Bit scanning and counting}

Counting leading ones:

\begin{verbatim}
  not a0, a0
  clz a0, a0
\end{verbatim}

Counting trailing ones:

\begin{verbatim}
  not a0, a0
  ctz a0, a0
\end{verbatim}

Counting bits cleared:

\begin{verbatim}
  not a0, a0
  pcnt a0, a0
\end{verbatim}

(This is better than XLEN-pcnt because RISC-V has no ``reverse-subtract-immediate'' operation.)

Odd parity:

\begin{verbatim}
  pcnt a0, a0
  andi a0, a0, 1
\end{verbatim}

Even parity:

\begin{verbatim}
  pcnt a0, a0
  addi a0, a0, 1
  andi a0, a0, 1
\end{verbatim}

(Using {\tt addi} here is better than using {\tt xori}, because there is
a compressed opcode for {\tt addi} but none for {\tt xori}.)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Test, set, and clear individual bits}

Extracting bit N:

\begin{verbatim}
  srli a0, a0, N
  andi a0, a0, 1
\end{verbatim}

Branching on bit N set:

\begin{verbatim}
  slli a0, a0, (XLEN-N-1)
  bltz a0, <bit_n_set>
\end{verbatim}

Branching on bit N clear:

\begin{verbatim}
  slli a0, a0, (XLEN-N-1)
  bgez a0, <bit_n_clear>
\end{verbatim}

Setting bit N (note that this {\tt li} is only one instruction on RV32, except when N=11 which requires two instructions):

\begin{verbatim}
  li a1, (1 << N)
  or a0, a0, a1
\end{verbatim}

Setting bit N without spilling:

\begin{verbatim}
  rori a0, a0, N
  ori a0, a0, 1
  rori a0, a0, 32-N
\end{verbatim}

(Or simply ``{\tt ori a0, 1 << N}'' if N is sufficiently small.)

Clearing bit N (note that this {\tt li} is only one instruction on RV32, except when N=11 which requires two instructions):

\begin{verbatim}
  li a1, (1 << N)
  andn a0, a0, a1
\end{verbatim}

Clearing bit N without spilling:

\begin{verbatim}
  rori a0, a0, N
  andi a0, a0, -2
  rori a0, a0, XLEN-N
\end{verbatim}

(Or simply ``{\tt andi a0, $\sim$(1 << N)}'' if N is sufficiently small.)

Setting bit N to the value in {\tt a1} (assuming a1 already is either 0 or 1):

\begin{verbatim}
  rori a0, a0, N
  andi a0, a0, -2
  or a0, a0, a1
  rori a0, a0, XLEN-N
\end{verbatim}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Funnel shifts}
\label{funnel}

A funnel shift takes two XLEN registers, concatenates them to a $2 \times
\textrm{XLEN}$ word, shifts that by a certain amount, then returns the lower
half of the result for a right shift and the upper half of the result for a
left shift.

The {\tt fsl}, {\tt fsr}, and {\tt fsri} instructions perform funnel shifts.

\subsection{Bigint shift}

A common application for funnel shifts is shift operations in bigint libraries.

For example, the following functions implement rotate-shift operations
for bigints made from {\tt n} XLEN words.

\begin{minipage}{\linewidth}
\begin{verbatim}
  void bigint_rol(uint_xlen_t data[], int n, int shamt)
  {
    if (n <= 0)
      return;

    uint_xlen_t buffer = data[n-1];
    for (int i = n-1; i > 0; i--)
      data[i] = fsl(data[i], shamt, data[i-1]);
    data[0] = fsl(data[0], shamt, buffer);
  }

  void bigint_ror(uint_xlen_t data[], int n, int shamt)
  {
    if (n <= 0)
      return;

    uint_xlen_t buffer = data[0];
    for (int i = 0; i < n-1; i++)
      data[i] = fsr(data[i], shamt, data[i+1]);
    data[n-1] = fsr(data[n-1], shamt, buffer);
  }
\end{verbatim}
\end{minipage}

These version only works for shift-amounts $<$XLEN. But functions supporting
other kinds of shift operations, or shifts $\ge$XLEN can easily be built
with {\tt fsl} and {\tt fsr}.

\subsection{Parsing bit-streams}

The following function parses {\tt n} 27-bit words from a packed array of XLEN words:

\begin{minipage}{\linewidth}
\begin{verbatim}
  void parse_27bit(uint_xlen_t *idata, uint_xlen_t *odata, int n)
  {
    uint_xlen_t lower = 0, upper = 0;
    int reserve = 0;

    while (n--) {
      if (reserve < 27) {
        uint_xlen_t buf = *(idata++);
        lower |= sll(buf, reserve);
        upper = reserve ? srl(buf, -reserve) : 0;
        reserve += XLEN;
      }
      *(odata++) = lower & ((1 << 27)-1);
      lower = fsr(lower, 27, upper);
      upper = srl(upper, 27);
      reserve -= 27;
    }
  }
\end{verbatim}
\end{minipage}

And here the same thing in RISC-V assembler:

\begin{minipage}{\linewidth}
\begin{verbatim}
  parse_27bit:
    li t1, 0              ; lower
    li t2, 0              ; upper
    li t3, 0              ; reserve
    li t4, 27             ; shamt
    slo t5, zero, t4      ; mask
    beqz a2, endloop      ; while (n--)
  loop:
    addi a2, a2, -1
    bge t3, t4, output       ; if (reserve < 27)
    lw t6, 0(a0)                 ; buf = *(idata++)
    addi a0, a0, 4
    sll t7, t6, t3               ; lower |= sll(buf, reserve)
    or t1, t1, t7
    sub t7, zero, t3             ; upper = reserve ? srl(buf, -reserve) : 0
    srl t7, t6, t7
    cmov t2, t3, t7, zero
    addi t3, t3, 32              ; reserve += XLEN;
  output:
    and t6, t1, t5           ; *(odata++) = lower & ((1 << 27)-1)
    sw t6, 0(a1)
    addi a1, a1, 4
    fsr t1, t1, t2, t4       ; lower = fsr(lower, 27, upper)
    srl t2, t2, t4           ; upper = srl(upper, 27)
    sub t3, t3, t4           ; reserve -= 27
    bnez a2, loop         ; while (n--)
  endloop:
    ret
\end{verbatim}
\end{minipage}

A loop iteration without fetch is 9 instructions long, and a loop iteration
with fetch is 17 instructions long.

Without ternary operators that would be 13 instructions and 22 instructions,
i.e. assuming one cycle per instruction, that function would be about 30\%
slower without ternary instructions.

\subsection{Fixed-point multiply}

A fixed-point multiply is simply an integer multiply, followed by a right
shift. If the entire dynamic range of XLEN bits should be useable for the
factors, then the product before shift must be 2*XLEN wide. Therefore {\tt
mul}+{\tt mulh} is needed for the multiplication, and funnel shift instructions
can help with the final right shift. For fixed-point numbers with N fraction
bits:

\begin{minipage}{\linewidth}
\begin{verbatim}
  mul_fracN:
    mulh a2, a0, a1
    mul a0, a0, a1
    fsri a0, N, a0, a2
    ret
\end{verbatim}
\end{minipage}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Arbitrary bit permutations}

This section lists code snippets for computing arbitrary bit permutations that
are defined by data (as opposed to bit permutations that are known at compile
time and can likely be compiled into shift-and-mask operations and/or a few
instances of bext/bdep).

\subsection{Using butterfly operations}
\label{butterfly}

The following macro performs a stage-{\tt N} butterfly operation on the word in
{\tt a0} using the mask in {\tt a1}.

\begin{verbatim}
  grevi a2, a0, (1 << N)
  cmix a0, a1, a2, a0
\end{verbatim}

The bitmask in {\tt a1} must be preformatted correctly for the selected butterfly
stage. A butterfly operation only has a XLEN/2 wide control word. The following
macros format the mask assuming those XLEN/2 bits in the lower half of {\tt a1}
on entry (preformatted mask in {\tt a1} on exit):

\begin{verbatim}
bfly_msk_0:
  zip a1, a1
  slli a2, a1, 1
  or a1, a1, a2

bfly_msk_1:
  zip2 a1, a1
  slli a2, a1, 2
  or a1, a1, a2

bfly_msk_2:
  zip4 a1, a1
  slli a2, a1, 4
  or a1, a1, a2

...
\end{verbatim}

A sequence of $2\cdot{}log_2(\textrm{XLEN})-1$ butterfly operations can perform any
arbitrary bit permutation (Bene{\v s} network):

\begin{verbatim}
  butterfly(LOG2_XLEN-1)
  butterfly(LOG2_XLEN-2)
  ...
  butterfly(0)
  ...
  butterfly(LOG2_XLEN-2)
  butterfly(LOG2_XLEN-1)
\end{verbatim}


Many permutations arising from real-world applications can be implemented
using shorter sequences. For example, any sheep-and-goats operation with either
the sheep or the goats bit reversed can be implemented in $log_2(\textrm{XLEN})$
butterfly operations.

Reversing a permutation implemented using butterfly operations is as simple as
reversing the order of butterfly operations.

% References
% http://www.princeton.edu/~rblee/PUpapers/xiao_spie00.pdf
% https://www.lirmm.fr/arith18/papers/hilewitz-PerformingBitManipulations.pdf
% https://pdfs.semanticscholar.org/bcd0/8fdccf3d5ab959fd81162bd811706ba1676a.pdf

\subsection{Using omega-flip networks}

The omega operation is a stage-0 butterfly preceded by a zip operation:

\begin{verbatim}
  zip a0, a0
  grevi a2, a0, 1
  cmix a0, a1, a2, a0
\end{verbatim}

The flip operation is a stage-0 butterfly followed by an unzip operation:

\begin{verbatim}
  grevi a2, a0, 1
  cmix a0, a1, a2, a0
  unzip a0, a0
\end{verbatim}

A sequence of $log_2(\textrm{XLEN})$ omega operations followed by
$log_2(\textrm{XLEN})$ flip operations can implement any arbitrary 32 bit
permutation.

As for butterfly networks, permutations arising from real-world applications
can often be implemented using a shorter sequence.

% References
% https://ieeexplore.ieee.org/document/878264/
% https://www.princeton.edu/~rblee/ELE572Papers/lee_slideshotchips2002.pdf

\subsection{Using baseline networks}

Another way of implementing arbitrary 32 bit permutations is using a
baseline network followed by an inverse baseline network.

A baseline network is a sequence of $log_2(\textrm{XLEN})$ butterfly(0)
operations interleaved with unzip operations. For example, a 32-bit
baseline network:

\begin{verbatim}
  butterfly(0)
  unzip
  butterfly(0)
  unzip.h
  butterfly(0)
  unzip.b
  butterfly(0)
  unzip.n
  butterfly(0)
\end{verbatim}

An inverse baseline network is a sequence of $log_2(\textrm{XLEN})$ butterfly(0)
operations interleaved with zip operations. The order is opposite to the
order in a baseline network. For example, a 32-bit inverse baseline network:

\begin{verbatim}
  butterfly(0)
  zip.n
  butterfly(0)
  zip.b
  butterfly(0)
  zip.h
  butterfly(0)
  zip
  butterfly(0)
\end{verbatim}

A baseline network followed by an inverse baseline network can implement
any arbitrary bit permutation.

% References
% https://dl.acm.org/citation.cfm?id=1311797

\subsection{Using sheep-and-goats}

The Sheep-and-goats (SAG) operation is a common operation for bit permutations.
It moves all the bits selected by a mask (goats) to the LSB end of the word
and all the remaining bits (sheep) to the MSB end of the word, without changing
the order of sheep or goats.

The SAG operation can easily be performed using {\tt bext} (data in {\tt a0} and
mask in {\tt a1}):

\begin{verbatim}
  bext a2, a0, a1
  not a1, a1
  bext a0, a0, a1
  pcnt a1, a1
  ror a0, a0, a1
  or a0, a0, a2
\end{verbatim}

Any arbitrary bit permutation can be implemented in $log_2(\textrm{XLEN})$ SAG
operations.

{\it The Hacker's Delight} describes an optimized standard C implementation of
the SAG operation. Their algorithm takes 254 instructions (for 32 bit) or 340
instructions (for 64 bit) on their reference RISC instruction
set.~\cite[p.~152f,~162f]{Seander05}

% References
% Knuth
% Hackers Delight, Chapter 7-7

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Mirroring and rotating bitboards}

Bitboards are 64-bit bitmasks that are used to represent part of the game state
in chess engines (and other board game AIs). The bits in the bitmask correspond
to squares on a $8 \times 8$ chess board:

\begin{verbatim}
 56 57 58 59 60 61 62 63
 48 49 50 51 52 53 54 55
 40 41 42 43 44 45 46 47
 32 33 34 35 36 37 38 39
 24 25 26 27 28 29 30 31
 16 17 18 19 20 21 22 23
  8  9 10 11 12 13 14 15
  0  1  2  3  4  5  6  7
\end{verbatim}

Many bitboard operations are simple straight-forward operations such as
bitwise-AND, but mirroring and rotating bitboards can take up to 20
instructions on x86.

\subsection{Mirroring bitboards}

Flipping horizontally or vertically can easily done with {\tt grevi}:

\begin{verbatim}
Flip horizontal:
 63 62 61 60 59 58 57 56    RISC-V Bitmanip:
 55 54 53 52 51 50 49 48       brev.b
 47 46 45 44 43 42 41 40
 39 38 37 36 35 34 33 32
 31 30 29 28 27 26 25 24    x86:
 23 22 21 20 19 18 17 16       13 operations
 15 14 13 12 11 10  9  8
  7  6  5  4  3  2  1  0

Flip vertical:
  0  1  2  3  4  5  6  7    RISC-V Bitmanip:
  8  9 10 11 12 13 14 15       bswap
 16 17 18 19 20 21 22 23
 24 25 26 27 28 29 30 31
 32 33 34 35 36 37 38 39    x86:
 40 41 42 43 44 45 46 47       bswap
 48 49 50 51 52 53 54 55
 56 57 58 59 60 61 62 63
\end{verbatim}

Rotating by 180 (flip horizontal and vertical):

\begin{verbatim}
Rotate 180:
  7  6  5  4  3  2  1  0    RISC-V Bitmanip:
 15 14 13 12 11 10  9  8       brev
 23 22 21 20 19 18 17 16
 31 30 29 28 27 26 25 24
 39 38 37 36 35 34 33 32    x86:
 47 46 45 44 43 42 41 40       14 operations
 55 54 53 52 51 50 49 48
 63 62 61 60 59 58 57 56
\end{verbatim}

\subsection{Rotating bitboards}

Using {\tt zip} a bitboard can be transposed easily:
\label{transposebitboard}

\begin{verbatim}
Transpose:
  7 15 23 31 39 47 55 63    RISC-V Bitmanip:
  6 14 22 30 38 46 54 62       zip, zip, zip
  5 13 21 29 37 45 53 61
  4 12 20 28 36 44 52 60
  3 11 19 27 35 43 51 59    x86:
  2 10 18 26 34 42 50 58       18 operations
  1  9 17 25 33 41 49 57
  0  8 16 24 32 40 48 56
\end{verbatim}

A rotation is simply the composition of a flip operation and a transpose
operation. This takes 19 operations on x86~\cite{ChessProg}. With Bitmanip
the rotate operation only takes 4 operations:

\begin{verbatim}
rotate_bitboard:
  bswap a0, a0
  zip a0, a0
  zip a0, a0
  zip a0, a0
\end{verbatim}

\subsection{Explanation}

The bit indices for a 64-bit word are 6 bits wide. Let $\texttt{i[5:0]}$ be the
index of a bit in the input, and let $\texttt{i$'$[5:0]}$ be the index of the
same bit after the permutation.

As an example, a rotate left shift by $N$ can be expressed using this notation
as $\texttt{i$'$[5:0]} = \texttt{i[5:0]} + N \,\,\, (\textrm{mod 64})$.

The GREV operation with shamt $N$ is $\texttt{i$'$[5:0]} = \texttt{i[5:0]} \textrm{ XOR } N$.

And a GZIP operation corresponds to a rotate left shift by one position of any
continuous region of $\texttt{i[5:0]}$. For example, {\tt zip} is a left rotate shift
of the entire bit index:

$$\texttt{i$'$[5:0]} = \{ \texttt{i[4:0]},\, \texttt{i[5]} \}$$

And {\tt zip4} performs a left rotate shift on bits {\tt 5:2}:

$$\texttt{i$'$[5:0]} = \{ \texttt{i[4:2]},\, \texttt{i[5]},\, \texttt{i[1:0]} \}$$

In a bitboard, $\texttt{i[2:0]}$ corresponds to the X coordinate of a board position, and
$\texttt{i[5:3]}$ corresponds to the Y coordinate.

Therefore flipping the board horizontally is the same as negating bits $\texttt{i[2:0]}$,
which is the operation performed by {\tt grevi rd, rs, 7} ({\tt brev.b}).

Likewise flipping the board vertically is done by {\tt grevi rd, rs, 56} ({\tt bswap}).

Finally, transposing corresponds by swapping the lower and upper half of $\texttt{i[5:0]}$,
or rotate shifting $\texttt{i[5:0]}$ by 3 positions. This can easily done by rotate shifting the entire
$\texttt{i[5:0]}$ by one bit position ({\tt zip}) three times.

\subsection{Rotating Bitcubes}

Let's define a bitcube as a $4 \times 4 \times 4$ cube with $x=\texttt{i[1:0]}$,
$y=\texttt{i[3:2]}$, and $z=\texttt{i[5:4]}$. Using the same methods as described
above we can easily rotate a bitcube by 90$^\circ$ around the X-, Y-, and Z-axis:

\begin{multicols}{3}
\begin{minipage}{\linewidth}
\begin{verbatim}
rotate_x:
  hswap a0, a0
  zip4 a0, a0
  zip4 a0, a0
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
rotate_y:
  brev.n a0, a0
  zip a0, a0
  zip a0, a0
  zip4 a0, a0
  zip4 a0, a0
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
rotate_z:
  nswap.h
  zip.h a0, a0
  zip.h a0, a0
\end{verbatim}
\end{minipage}
\end{multicols}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Rank and select}

Rank and select are fundamental operations in succinct data structures~\cite{SelectX86}.

\texttt{select(a0, a1)} returns the position of the \texttt{a1}th set bit in \texttt{a0}.
It can be implemented efficiently using \texttt{bdep} and \texttt{ctz}:

\begin{minipage}{\linewidth}
\begin{verbatim}
  select:
    li a2, 1
    sll a1, a2, a1
    bdep a0, a1, a0
    ctz a0, a0
    ret
\end{verbatim}
\end{minipage}

\texttt{rank(a0, a1)} returns the number of set bits in \texttt{a0} up to and
including position \texttt{a1}.

\begin{minipage}{\linewidth}
\begin{verbatim}
  rank:
    not a1, a1
    sll a0, a1
    pcnt a0, a0
    ret
\end{verbatim}
\end{minipage}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Inverting Xorshift RNGs}

Xorshift RNGs are a class of fast RNGs for different bit widths. There are 648
Xorshift RNGs for 32 bits, but this is the one that the author of the original
Xorshift RNG paper recommends.~\cite[p. 4]{Xorshift}

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint32_t xorshift32(uint32_t x)
  {
    x ^= x << 13;
    x ^= x >> 17;
    x ^= x << 5;
    return x;
  }
\end{verbatim}
\end{minipage}

This function of course has been designed and selected so it's efficient, even
without special bit-manipulation instructions. So let's look at the inverse
instead. First, the na\"ive form of inverting this function:

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint32_t xorshift32_inv(uint32_t x)
  {
    uint32_t t;
    t = x ^ (x << 5);
    t = x ^ (t << 5);
    t = x ^ (t << 5);
    t = x ^ (t << 5);
    t = x ^ (t << 5);
    x = x ^ (t << 5);
    x = x ^ (x >> 17);
    t = x ^ (x << 13);
    x = x ^ (t << 13);
    return x;
  }
\end{verbatim}
\end{minipage}

This translates to 18 RISC-V instructions, not including the function call overhead.

Obviously the C expression {\tt x \^{} (x >> 17)} is already its own inverse
(because $17 \ge XLEN/2$) and therefore already has an effecient inverse. But the two
other blocks can easily be implemented using a single {\tt clmul} instruction each:

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint32_t xorshift32_inv(uint32_t x)
  {
    x = clmul(x, 0x42108421);
    x = x ^ (x >> 17);
    x = clmul(x, 0x04002001);
    return x;
  }
\end{verbatim}
\end{minipage}

This are 8 RISC-V instructions, including 4 instructions for loading the
constants, but not including the function call overhead.

An optimizing compiler could easily generate the clmul instructions and the magic
constants from the C code for the na\"ive implementation. ({\tt 0x04002001 = (1 << 2*13) | (1 << 13) | 1}
and {\tt 0x42108421 = (1 << 6*5) | (1 << 5*5) | \dots | (1 << 5) | 1})

The obvious remaining question is ``if {\tt clmul(x, 0x42108421)} is the inverse of {\tt x \^{} (x << 5)}, what's
the inverse of {\tt x \^{} (x >> 5)}?'' It's {\tt clmulhx(x, 0x08421084)}, where {\tt 0x08421084 == 0x42108421 << (clz(0x42108421)+1)} (mod $2^{32}$).

A special case of xorshift is {\tt x \^{} (x >> 1)}, which is a gray encoder. The corresponding gray decoder is {\tt clmulhx(x, 0xffffffff)}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Fill right of most significant set bit}

The ``fill right'' or ``fold right'' operation is a pattern commonly used in bit manipulation code.~\cite{MAGIC}

The straight-forward RV64 implementation requires 12 instructions:

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint64_t rfill(uint64_t x)
  {
    x |= x >> 1;   // SRLI, OR
    x |= x >> 2;   // SRLI, OR
    x |= x >> 4;   // SRLI, OR
    x |= x >> 8;   // SRLI, OR
    x |= x >> 16;  // SRLI, OR
    x |= x >> 32;  // SRLI, OR
    return x;
  }
\end{verbatim}
\end{minipage}

With {\tt clz} it can be implemented in only 4 instructions. Notice the
handling of the case where {\tt x=0} using {\tt sltiu+addi}.

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint64_t rfill_clz(uint64_t x)
  {
    uint64_t t;
    t = clz(x);         // CLZ
    x = (!x)-1;         // SLTIU, ADDI
    x = x >> (t & 63);  // SRL
    return x;
  }
\end{verbatim}
\end{minipage}

Alternatively, a Trailing Bit Manipulation (TBM) code pattern can be used
together with {\tt brev} to implement this function in 5 instructions:

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint64_t rfill_brev(uint64_t x)
  {
    x = brev(x);        // GREVI
    x = (x - 1) & ~x;   // ADDI, ANDN
    x = ~x;             // NOT
    x = brev(x);        // GREVI
    return x;
  }
\end{verbatim}
\end{minipage}

Finally, there is another implementation in 4 instructions using BMATOR, if we do
not count the extra instructions for loading utility matrices.

\begin{minipage}{\linewidth}
\begin{verbatim}
  uint64_t rfill_bmat(uint64_t x)
  {
    uint64_t m0, m1, m2, t;

    m0 = 0xFF7F3F1F0F070301LL;  // LD
    m1 = bmatflip(m0 << 8);     // SLLI, BMATFLIP
    m2 = -1LL;                  // ADDI

    t = bmator(x, m0);          // BMATOR
    x = bmator(x, m2);          // BMATOR
    x = bmator(m1, x);          // BMATOR
    x |= t;                     // OR

    return x;
  }
\end{verbatim}
\end{minipage}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Decoding RISC-V Immediates}

The following code snippets decode and sign-extend the immediate from RISC-V
S-type, B-type, J-type, and CJ-type instructions. They are nice ``nothing up my
sleeve''-examples for real-world bit permutations.

\begin{small}
\begin{center}
\begin{tabular}{p{0in}p{0.4in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.4in}p{0.6in}p{0.4in}p{0.6in}p{0.7in}l}
& & & & & & & & & & \\
                      &
\multicolumn{1}{l}{\instbit{31}} &
\multicolumn{1}{r}{\instbit{27}} &
\instbit{26} &
\instbit{25} &
\multicolumn{1}{l}{\instbit{24}} &
\multicolumn{1}{r}{\instbit{20}} &
\instbitrange{19}{15} &
\instbitrange{14}{12} &
\instbitrange{11}{7} &
\instbitrange{6}{0} \\
\cline{2-11}

&
\multicolumn{4}{|c|}{imm[11:5]} &
\multicolumn{2}{c|}{} &
\multicolumn{1}{c|}{} &
\multicolumn{1}{c|}{} &
\multicolumn{1}{c|}{imm[4:0]} &
\multicolumn{1}{c|}{} & S-type \\
\cline{2-11}

&
\multicolumn{4}{|c|}{imm[12$\vert$10:5]} &
\multicolumn{2}{c|}{} &
\multicolumn{1}{c|}{} &
\multicolumn{1}{c|}{} &
\multicolumn{1}{c|}{imm[4:1$\vert$11]} &
\multicolumn{1}{c|}{} & B-type \\
\cline{2-11}

&
\multicolumn{8}{|c|}{imm[20$\vert$10:1$\vert$11$\vert$19:12]} &
\multicolumn{1}{c|}{} &
\multicolumn{1}{c|}{} & J-type \\
\cline{2-11}

\end{tabular}

\begin{tabular}{p{0in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}p{0.05in}l}
& & & & & & & & & & \\
                      &
\instbit{15} &
\instbit{14} &
\instbit{13} &
\multicolumn{1}{c}{\instbit{12}} &
\instbit{11} &
\instbit{10} &
\instbit{9} &
\instbit{8} &
\instbit{7} &
\instbit{6} &
\multicolumn{1}{c}{\instbit{5}} &
\instbit{4} &
\instbit{3} &
\instbit{2} &
\instbit{1} &
\instbit{0} \\
\cline{2-17}

&
\multicolumn{3}{|c|}{} &
\multicolumn{11}{c|}{imm[11$\vert$4$\vert$9:8$\vert$10$\vert$6$\vert$7$\vert$3:1$\vert$5]} &
\multicolumn{2}{c|}{} & CJ-type \\
\cline{2-17}

\end{tabular}
\end{center}
\end{small}

\begin{multicols}{2}
\begin{minipage}{\linewidth}
\begin{verbatim}
  decode_s:
    li t0, 0xfe000f80
    bext a0, a0, t0
    c.slli a0, 20
    c.srai a0, 20
    ret
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
  decode_b:
    li t0, 0xeaa800aa
    rori a0, a0, 8
    grevi a0, a0, 8
    shfli a0, a0, 7
    bext a0, a0, t0
    c.slli a0, 20
    c.srai a0, 19
    ret
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
  decode_j:
    li t0, 0x800003ff
    li t1, 0x800ff000
    bext a1, a0, t1
    c.slli a1, 23
    rori a0, a0, 21
    bext a0, a0, t0
    c.slli a0, 12
    c.or a0, a1
    c.srai a0, 11
    ret
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
  // variant 1 (with RISC-V Bitmanip)
  decode_cj:
    li t0, 0x28800001
    li t1, 0x000016b8
    li t2, 0xb4e00000
    li t3, 0x4b000000
    bext a1, a0, t1
    bdep a1, a1, t2
    rori a0, a0, 11
    bext a0, a0, t0
    bdep a0, a0, t3
    c.or a0, a1
    c.srai a0, 20
    ret
\end{verbatim}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{verbatim}
  // variant 2 (without RISC-V Bitmanip)
  decode_cj:
    srli a5, a0, 2
    srli a4, a0, 7
    c.andi a4, 16
    slli a3, a0, 3
    c.andi a5, 14
    c.add a5, a4
    andi a3, a3, 32
    srli a4, a0, 1
    c.add a5, a3
    andi a4, a4, 64
    slli a2, a0, 1
    c.add a5, a4
    andi a2, a2, 128
    srli a3, a0, 1
    slli a4, a0, 19
    c.add a5, a2
    andi a3, a3, 768
    c.slli a0, 2
    c.add a5, a3
    andi a0, a0, 1024
    c.srai a4, 31
    c.add a5, a0
    slli a0, a4, 11
    c.add a0, a5
    ret
\end{verbatim}
\end{minipage}
\end{multicols}

Or using XBitfield:

\begin{multicols}{2}
\begin{verbatim}
  decode_s:
    bfxp a0, a1, zero, 7, 5, 20
    bfxp a0, a1, a0, 25, 7, 25
    c.srai a0, 20
    ret

  decode_b:
    bfxp a0, a1, zero, 7, 1, 30
    bfxp a0, a1, a0, 25, 6, 24
    bfxp a0, a1, a0, 8, 4, 20
    bfxp a0, a1, a0, 31, 1, 31
    c.srai a0, 19
    ret

  decode_j:
    bfxp a0, a1, zero, 21, 10, 12
    bfxp a0, a1, a0, 20, 1, 22
    bfxp a0, a1, a0, 12, 8, 23
    bfxp a0, a1, a0, 31, 1, 31
    c.srai a0, 11
    ret

  decode_cj:
    bfxp a0, a1, zero, 11, 1, 24
    bfxp a0, a1, a0, 9, 2, 28
    bfxp a0, a1, a0, 8, 1, 30
    bfxp a0, a1, a0, 7, 1, 26
    bfxp a0, a1, a0, 6, 1, 27
    bfxp a0, a1, a0, 3, 3, 21
    bfxp a0, a1, a0, 2, 1, 25
    bfxp a0, a1, a0, 12, 1, 31
    c.srai a0, 20
    ret
\end{verbatim}
\end{multicols}
